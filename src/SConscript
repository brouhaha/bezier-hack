# Copyright 2023 Eric Smith
# SPDX-License-Identifier: GPL-3.0-only

import SCons

Import('env')

def find_file(filename, pathnames):
    paths = [env.Dir(pathname) for pathname in pathnames]
    return SCons.Node.FS.find_file(filename, paths)



env.Append(CXXFLAGS = ['-g', '--std=c++20'])

local_tools = ['qt6']
if env['target'] in ('win32', 'win64'):
    local_tools += ['wxs', 'msi']
for lt in local_tools:
    env.Tool(lt, toolpath = ['.'])


env.EnableQt6Modules(['QtCore',
                      'QtGui',
		      'QtWidgets'])

SOURCES = ['main.cc',
           'main_window.cc',
	   'central_widget.cc',
	   'cubic_bezier_item.cc',
	   'cubic_bezier_view.cc',
	   'cubic_bezier_point_item.cc',
	   'axes_item.cc',
	   'cubic_bezier_params.cc',
	   'double_line_edit.cc',
	   'cubic_bezier.cc',
	  ]


executable = env.Program('cubic-bezier-bounding-box', SOURCES)[0]

if env['target'] in ('win32', 'win64'):
    wxs_template = env.File('cubic-bezier-bounding_box.wxst')

    dll_names = ['Qt6Core',
                 'Qt6Gui',
                 'Qt6Widgets',
                 'libstdc++-6',
                 ]

    if env['target'] == 'win32':
        dll_names.append('libgcc_s_dw2-1')
    elif env['target'] == 'win64':
        dll_names.append('libgcc_s_seh-1')

    dlls = [find_file(dll_name + '.dll', env['DLLPATH']) for dll_name in dll_names]
    files = [executable] + dlls
    wxs = env.WXS('voyager-lcd.wxs', [wxs_template, files])
    env.MSI(wxs)
